import java.time.ZoneId
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

buildscript {
    ext.kotlinVersion = '1.3.61'
    ext.dropWizardVersion = '2.0.0'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id "com.heroku.sdk.heroku-gradle" version "1.0.1"
}

version '1.0'
group 'com.beust.cedlinks'
description 'CedLinks'

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.heroku.sdk.heroku-gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
        "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion",
        "com.google.inject:guice:4.2.0",
        "io.dropwizard:dropwizard-core:$dropWizardVersion",
        "com.squareup.retrofit2:retrofit:2.9.0",
        "com.squareup.retrofit2:converter-gson:2.9.0",
        "org.postgresql:postgresql:42.2.14.jre7",
        "org.jetbrains.exposed:exposed:0.12.1",
        "com.squareup.okhttp3:logging-interceptor:3.9.0"

    implementation platform("ru.vyarus:dropwizard-guicey:5.1.0")

    // no need to specify versions
    implementation 'ru.vyarus:dropwizard-guicey'

    implementation 'io.dropwizard:dropwizard-auth'
//    implementation 'com.google.inject:guice-assistedinject'

//    testImplementation 'io.dropwizard:dropwizard-test'
//    testImplementation 'org.spockframework:spock-core'

    testCompile "org.testng:testng:6.14.3"
}

mainClassName = 'com.beust.cedlinks.MainKt'
//def demoPort = System.getenv("PORT") ?: 80
//applicationDefaultJvmArgs = ["-Ddw.server.applicationConnectors[0].port=" + demoPort.toString()]

run {
    args = ['server', 'config.yml']
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': rootProject.version
        attributes 'Implementation-Vendor-Id': rootProject.group
        attributes 'Build-Time': ZonedDateTime.now(ZoneId.of("UTC"))
                .format(DateTimeFormatter.ISO_ZONED_DATE_TIME)
        attributes 'Built-By': InetAddress.localHost.hostName
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': mainClassName
    }
}

// Heroku

task stage(dependsOn: ['build', 'shadowJar', 'clean'])

task copyToLib(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile)
}

stage.dependsOn(copyToLib)

heroku {
    appName = 'ced-links'
}